use v6;
use Test;
use NativeHelpers::iovec;
use NativeCall;

sub writev(int32 $fd, iovecs $iov, int32 $iovcnt) returns ssize_t is native { ... }
sub readv(int32 $fd, iovecs $iov, int32 $iovcnt) returns ssize_t is native { ... }
sub lseek(int32 $fd, int32 $offset, int32 $whence) returns int32 is native { ... }
my $val = ('A'..'Z').pick(10).join('');
my $data = (^1000).pick;
my $handle = open $*TMPDIR.add($val).IO, :r, :w;

my $rbufs = iovecs.new(2);
my $wbufs = iovecs.allocate(2);

$wbufs[0] = iovec.new($val.substr(0, 5));
$wbufs[1] = iovec.new($val.substr(5));

$rbufs[0] = iovec.new(5);
$rbufs[1] = iovec.new(5);

my $res = writev($handle.native-descriptor, $wbufs, $wbufs.elems);
is $res, 10, "Wrote temp data to file";
lseek($handle.native-descriptor, 0, 0);
is $handle.lines[0], $val, "data written matches";

lseek($handle.native-descriptor, 0, 0);
$res = readv($handle.native-descriptor, $rbufs, $rbufs.elems);
is $res, 10, "Read temp data from file";
subtest {
  is $rbufs[0].Str, $val.substr(0, 5), <First iovec>;
  is $rbufs[1].Str, $val.substr(5), <Second iovec>;
}, "Get temp data back from file";

$wbufs.free;
$rbufs.free;
$handle.close;
unlink($*TMPDIR.add($val).IO);
done-testing;
